USE_LR_PARSER

program

(* ====================================== Statements ========= *)
<prog>               ::= <stmtList>                          .

<stmtList> ::= <epsilon>                                        
            | <stmt> ";" <stmtList>                             .

<epsilon>  ::= .

<stmt> ::= <dec> 
        | <block> 
        | <conditional> 
        | <whileLoop> 
        | <forLoop>
        | <printStmt>
        | <increment>                                                .

<dec> ::= "int" id 
      |  "bool" id                                              .

<assign> ::= id "=" <expr>                                      .

<block> ::= "{" <stmtList> "}"                                  .

<conditional> ::= "if" <expr> "then" <block> 
                | "if" <expr> "then" <block> "else" <block>     .

<whileLoop> ::= "while" "(" <expr> ")" <block>                  .

<forLoop> ::= "for" "(" <assign> ";" <expr> ";" <increment> ")" <block> .

<increment> ::= <ids> 
             | <assign>                                         .

<printStmt> ::= "print" "(" <expr> ")"                          .

(* ====================================== Expressions ======== *)
<expr> ::= <disjunction>                                        .

<disjunction> ::= <conjunction> 
                | <disjunction> "||" <conjunction>              .

<conjunction> ::= <equalitytest> 
                | <conjunction> "&&" <equalitytest>             .

<equalitytest> ::= <addsub> 
                | <equalitytest> "<" <addsub> 
                | <equalitytest> ">" <addsub> 
                | <equalitytest> ">=" <addsub> 
                | <equalitytest> "<=" <addsub> 
                | <equalitytest> "==" <addsub> 
                | <equalitytest> "!=" <addsub>                  .

<addsub> ::= <multdivmod> 
            | <addsub> "+" <multdivmod> 
            | <addsub> "-" <multdivmod>                         .

<multdivmod> ::= <unaryminus> 
                | <multdivmod> "*" <unaryminus> 
                | <multdivmod> "div" <unaryminus> 
                | <multdivmod> "mod" <unaryminus>               .

<unaryminus> ::= <exponent> 
                | "-" <unaryminus>                              .

<exponent> ::= <notexpr> 
              | <notexpr> "^" <exponent>                        .

<notexpr> ::= <factor> 
            | "!" <notexpr>                                     .

<factor> ::= "(" <expr> ")" 
            |  "|" <expr> "|" 
            | integer 
            | boolean 
            | <ids>                                             .

<ids> ::= id 
       | "++" id 
       | id "++" 
       | "--" id 
       | id "--"                                           .